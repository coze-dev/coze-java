plugins {
    id 'com.diffplug.spotless' version '6.11.0' apply false
}

allprojects {
    apply plugin: 'java'

    group = 'com.bytedance'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation"
    }
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'

    spotless {
        java {
            removeUnusedImports()
            googleJavaFormat('1.15.0')  // 指定一个兼容 Java 8 的版本
            endWithNewline()
            trimTrailingWhitespace()
            importOrder('java', 'javax', 'org', 'com', '')
            licenseHeader '/* (C)$YEAR */'
        }
    }

    checkstyle {
        toolVersion = '8.45.1'  // 使用支持 Java 8 的版本
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        maxWarnings = 0
    }
}