plugins {
    id 'com.diffplug.spotless' version '6.11.0' apply false
    id 'jacoco'
}

allprojects {
    apply plugin: 'java'

    group = 'com.bytedance'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

}


subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation"
    }
    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            removeUnusedImports()
            googleJavaFormat('1.7')
            endWithNewline()
            trimTrailingWhitespace()
            importOrder('java', 'javax', 'org', 'com', '')
            licenseHeader '/* (C)$YEAR */'
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    jacocoTestReport {
        dependsOn test
        
        reports {
            xml.required = false
            csv.required = false
            html.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    'com/coze/openapi/service/client/**',
                    '**/AudioClient.class',
                    '**/ChatClient.class',
                    '**/ConversationClient.class',
                    // 添加其他需要排除的具体类
                ])
            }))
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.8
                }
            }
        }
    }
}
